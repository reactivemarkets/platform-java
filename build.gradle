/*
 * Copyright (C) 2020 Reactive Markets Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import io.netifi.flatbuffers.plugin.tasks.FlatBuffers

plugins {
    id 'application'
    id 'checkstyle'
    id 'io.netifi.flatbuffers' version '1.0.5'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.gradle.spotless' version '3.26.1'
}

ext {
    maven_username = System.getenv("MAVEN_USERNAME") ?: ""
    maven_password = System.getenv("MAVEN_PASSWORD") ?: ""
    gpg_private_key = System.getenv("GPG_PRIVATE_KEY") ?: ""
    gpg_password = System.getenv("GPG_PASSWORD") ?: ""
}

mainClassName = 'com.reactivemarkets.platform.example.feed.FeedGatewayL2Subscription'
defaultTasks 'build'
group 'com.reactivemarkets'

// The following JVM may be required to enable unsafe access in Java 11 and above.
/*
applicationDefaultJvmArgs = [
        '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED',
        '-Dio.netty.tryReflectionSetAccessible=true'
]
*/

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.reactivemarkets', name: 'papi', version: '1.2.0'
    compile group: 'com.google.flatbuffers', name: 'flatbuffers-java', version: '1.12.0'
    compile group: 'log4j', name: 'apache-log4j-extras', version: '1.2.+'
    compile group: 'org.agrona', name: 'agrona', version: '0.9.+'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.+'
    // required for websocket examples
    implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client-jdk:1.16'

    testCompile group: 'junit', name: 'junit', version: '4.+'
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

task buildFlatBuffers(type: FlatBuffers) {
    inputDir = file('src/main/resources/fbs')
    outputDir = file('src/main/java')
    language = 'java'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'platform-java'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Platform Java'
                description = 'The Reactive Platform API for Java.'
                url = 'https://opensource.reactivemarkets.com'
                developers {
                    developer {
                        id = 'marayl'
                        name = 'Mark Aylett'
                        email = 'mark.aylett@reactivemarkets.com'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:reactivemarkets/platform-java.git'
                    developerConnection = 'scm:git:ssh://github.com:reactivemarkets/platform-java.git'
                    url = 'https://github.com/reactivemarkets/platform-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${maven_username}"
                password = "${maven_password}"
            }
        }
    }
}

signing {
    useInMemoryPgpKeys("${gpg_private_key}", "${gpg_password}")
    sign(publishing.publications["mavenJava"])
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md'

        endWithNewline()
        indentWithSpaces()
        trimTrailingWhitespace()
    }
    java {
        target project.fileTree('src') {
            include '**/*.java'
            exclude '**/encoding/fbs/*.java'
        }
        endWithNewline()
        indentWithSpaces()
        trimTrailingWhitespace()

        licenseHeaderFile 'spotless.license.java'
        removeUnusedImports()
    }
}
